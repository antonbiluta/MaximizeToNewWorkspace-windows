SetWorkingDir(A_ScriptDir)

; Path to the DLL, relative to the script
VDA_PATH := A_ScriptDir . "\VirtualDesktopAccessor.dll"
hVirtualDesktopAccessor := DllCall("LoadLibrary", "Str", VDA_PATH, "Ptr")
; Function from DLL
GetDesktopCountProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "GetDesktopCount", "Ptr")
GoToDesktopNumberProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "GoToDesktopNumber", "Ptr")
GetCurrentDesktopNumberProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "GetCurrentDesktopNumber", "Ptr")
IsWindowOnCurrentVirtualDesktopProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "IsWindowOnCurrentVirtualDesktop", "Ptr")
IsWindowOnDesktopNumberProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "IsWindowOnDesktopNumber", "Ptr")
MoveWindowToDesktopNumberProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "MoveWindowToDesktopNumber", "Ptr")
IsPinnedWindowProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "IsPinnedWindow", "Ptr")
PinWindowProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "PinWindow", "Ptr")

GetDesktopNameProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "GetDesktopName", "Ptr")
SetDesktopNameProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "SetDesktopName", "Ptr")
CreateDesktopProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "CreateDesktop", "Ptr")
RemoveDesktopProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "RemoveDesktop", "Ptr")

; On change listeners
RegisterPostMessageHookProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "RegisterPostMessageHook", "Ptr")
UnregisterPostMessageHookProc := DllCall("GetProcAddress", "Ptr", hVirtualDesktopAccessor, "AStr", "UnregisterPostMessageHook", "Ptr")

GetDesktopCount() {
    global GetDesktopCountProc
    count := DllCall(GetDesktopCountProc, "Int")
    return count
}
MoveCurrentWindowToDesktop(number) {
    global MoveWindowToDesktopNumberProc, GoToDesktopNumberProc
    activeHwnd := WinGetID("A")
    DllCall(MoveWindowToDesktopNumberProc, "Ptr", activeHwnd, "Int", number, "Int")
    DllCall(GoToDesktopNumberProc, "Int", number, "Int")
}
GoToPrevDesktop() {
    global GetCurrentDesktopNumberProc, GoToDesktopNumberProc
    current := DllCall(GetCurrentDesktopNumberProc, "Int")
    last_desktop := GetDesktopCount() - 1
    ; If current desktop is 0, go to last desktop
    if (current = 0) {
        MoveOrGotoDesktopNumber(last_desktop)
    } else {
        MoveOrGotoDesktopNumber(current - 1)
    }
    return
}
GoToNextDesktop() {
    global GetCurrentDesktopNumberProc, GoToDesktopNumberProc
    current := DllCall(GetCurrentDesktopNumberProc, "Int")
    last_desktop := GetDesktopCount() - 1
    ; If current desktop is last, go to first desktop
    if (current = last_desktop) {
        MoveOrGotoDesktopNumber(0)
    } else {
        MoveOrGotoDesktopNumber(current + 1)
    }
    return
}
GoToDesktopNumber(num) {
    global GoToDesktopNumberProc
    DllCall(GoToDesktopNumberProc, "Int", num, "Int")
    return
}
MoveOrGotoDesktopNumber(num) {
    ; If user is holding down Mouse left button, move the current window also
    if (GetKeyState("LButton")) {
        MoveCurrentWindowToDesktop(num)
    } else {
        GoToDesktopNumber(num)
    }
    return
}
GetDesktopName(num) {
    global GetDesktopNameProc
    utf8_buffer := Buffer(1024, 0)
    ran := DllCall(GetDesktopNameProc, "Int", num, "Ptr", utf8_buffer, "Ptr", utf8_buffer.Size, "Int")
    name := StrGet(utf8_buffer, 1024, "UTF-8")
    return name
}
SetDesktopName(num, name) {
    global SetDesktopNameProc
    OutputDebug(name)
    name_utf8 := Buffer(1024, 0)
    StrPut(name, name_utf8, "UTF-8")
    ran := DllCall(SetDesktopNameProc, "Int", num, "Ptr", name_utf8, "Int")
    return ran
}
CreateDesktop() {
    global CreateDesktopProc
    ran := DllCall(CreateDesktopProc, "Int")
    return ran
}
RemoveDesktop(remove_desktop_number, fallback_desktop_number) {
    global RemoveDesktopProc
    ran := DllCall(RemoveDesktopProc, "Int", remove_desktop_number, "Int", fallback_desktop_number, "Int")
    return ran
}

; SetDesktopName(0, "It works! üê±")

DllCall(RegisterPostMessageHookProc, "Ptr", A_ScriptHwnd, "Int", 0x1400 + 30, "Int")
OnMessage(0x1400 + 30, OnChangeDesktop)
OnChangeDesktop(wParam, lParam, msg, hwnd) {
    Critical(1)
    OldDesktop := wParam + 1
    NewDesktop := lParam + 1
    Name := GetDesktopName(NewDesktop - 1)

    ; Use Dbgview.exe to checkout the output debug logs
    OutputDebug("Desktop changed to " Name " from " OldDesktop " to " NewDesktop)
    ; TraySetIcon(".\Icons\icon" NewDesktop ".ico")
}

; fullscreen mode
DesktopCount := -1
CurrentDesktop := -1
titleDesktop := ""
hide := False
desktopList := False
SetTimer unWrapWindow, 1000

Ctrl & Left::GoToPrevDesktop()
Ctrl & Right::GoToNextDesktop()
Ctrl & 1::MoveOrGotoDesktopNumber(0)
Ctrl & 2::MoveOrGotoDesktopNumber(1)
Ctrl & 3::MoveOrGotoDesktopNumber(2)
Ctrl & 4::MoveOrGotoDesktopNumber(3)
Ctrl & 5::MoveOrGotoDesktopNumber(4)

return

GetCurrentDesktop(){
    return DllCall(GetCurrentDesktopNumberProc, "Int")
}

updateVariable() {
    global DesktopCount, CurrentDesktop
    DesktopCount := GetDesktopCount()
    CurrentDesktop := GetCurrentDesktop()
    return
}

updateDesktopTitle() {
    global titleDesktop
    titleDesktop := GetDesktopName(CurrentDesktop)
    return
}

unWrapWindow(){
    updateVariable()

    try {
        activeHwnd := WinGetID("A")
        WinGetPos &X, &Y, &W, &H, activeHwnd
        title := activeHwnd
        style := WinGetStyle(activeHwnd)

        ; –ï—Å–ª–∏ —Ä–∞–∑–≤–µ—Ä–Ω—É–ª–∏, —Ç–æ –æ—Ç–∫—Ä—ã—Ç—å –Ω–∞ –æ—Ç–¥–µ–ª—å–Ω–æ–º –≤–æ—Ä–∫–µ
        if CurrentDesktop = 0 and (style & 0x1000000) and !(W < A_ScreenHeight) {
            newDesktop := CreateDesktop()
            SetDesktopName(newDesktop, title)
            MoveCurrentWindowToDesktop(newDesktop)
        }

        ; –ï—Å–ª–∏ —Å–≤–µ—Ä–Ω—É–ª–∏, –ø—Ä–æ–≤–µ—Ä–∏—Ç—å —Å–æ–±—Å—Ç–≤–µ–Ω–Ω–æ–µ –ª–∏ —ç—Ç–æ –æ–∫–Ω–æ, –µ—Å–ª–∏ –¥–∞ - —É–¥–∞–ª–∏—Ç—å
        if CurrentDesktop > 0 and !(style & 0x1000000) and (W < A_ScreenWidth) {
            updateDesktopTitle()
            if title = titleDesktop {
                RemoveDesktop(CurrentDesktop, 0)
            }
        }

        ; –ï—Å–ª–∏ –æ—Ç–∫—Ä—ã–ª–∏ –¥—Ä—É–≥–æ–µ –æ–∫–Ω–æ –Ω–∞ –ø–µ—Ä—Å–æ–Ω–∞–ª—å–Ω–æ–º (Use with MyDockFinder)
        if CurrentDesktop > 0 {
            updateDesktopTitle()
            currentHwnd := WinGetID("ahk_id " titleDesktop)
            activeHwnd := WinGetID("A")
            WinGetPos &X, &Y, &W, &H, activeHwnd

            if !(activeHwnd = currentHwnd) {
                MoveCurrentWindowToDesktop(0)
            } 
        }

    }
    catch TargetError
        OutputDebug A_Now ": Window wait.."
    finally
        updateVariable()

    return
}